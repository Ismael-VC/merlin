(
@|labels )

@get-sublabel ( name* -- sublabel* )
  DUP2 slen ;sublabel slen ADD2 #0030 LTH2 ?{ ;Error/sublabel !<set-error> }
  [ LIT2 &ptr $2 ] <scpy>
  ;sublabel JMP2r

@<set-scope> ( t* -- )
  ( | copy scope until sublabel )
  DUP2 [ LIT2r =scope ]
  &>w ( -- )
    LDAk [ LIT "/ ] EQU ?&end
    LDAk STHkr STA
    INCr INC2 LDAk ?&>w

  &end POP2 #00 STHr STA
    ( | prepare sublabel pointer )
    ;scope ;sublabel <scpy>
    [ LIT2 "/ 00 ] ;sublabel scap/
    ( ptr ) INC2k ,get-sublabel/ptr STR2
    ( cap ) STA2
    ( >> )

@<create-symbol> ( name* -- )
  ;scan LDA ?{ POP2 JMP2r }
  is-hex ?&invalid
  is-opcode ?&invalid
  is-runic ?&invalid
  DUP2 find-symbol INC2 ORA ?&not-unique
  ( addr* ) ;heap-ptr LDA2 [ LIT2 &ptr =mem/symbols ] STH2k INC2r INC2r STA2
  ( refs ) #00 STH2kr INC2r STA
  ( name[] ) DUP2 STH2kr <scpy>
  slen STH2r ADD2 INC2 ,&ptr STR2
  [ LIT2 &count $2 ] INC2 ,&count STR2
  JMP2r

  &invalid ( name* -- )
    POP2 ;Error/symbol !<set-error>

  &not-unique ( name* -- )
    POP2 ;Error/duplicate !<set-error>

@find-symbol ( name* -- <addr>* )
  ,&t STR2
  ;<create-symbol>/ptr LDA2 ;mem/symbols
  &>l ( -- )
    EQU2k ?&end
    #0003 ADD2 DUP2 [ LIT2 &t $2 ] scmp ?&found
    scap/ INC2 GTH2k ?&>l
  &end POP2 POP2 #ffff JMP2r
  &found ( symbols* -- <addr>* )
  #0003 SUB2 NIP2 JMP2r

@get-any ( str* -- value* )
  is-hex ?shex
  !get-ref/eager

@get-ref ( token* -- addr* )
  LDAk [ LIT "{ ] NEQ ?{ POP2 Lambda/push }
  ;scan LDA ?&scan
  &eager ( -- )
    LDAk [ LIT "/ ] NEQ ?{ INC2 get-sublabel }
    LDAk [ LIT "& ] NEQ ?{ INC2 get-sublabel }
    find-symbol INC2k #0000 EQU2 ?{
      INC2k INC2 LDAk INC ROT ROT STA
      LDA2 JMP2r }
    ;Error/reference <set-error>
    &scan JMP2r

@get-rel ( label* -- distance )
  get-ref ;heap-ptr LDA2 INC2 INC2 SUB2
  ( ) DUP2 #0080 ADD2 POP ?{ NIP JMP2r }
  ( ) ;scan LDA ?{ ;Error/distance <set-error> }
  POP2 #ff JMP2r

@Lambda [
  &push ( -- name* )
    [ LIT &count $1 ] INCk ,&count STR
    DUP [ LIT2 &ptr =&buf ] INC2k ,&ptr STR2
    STA
    ( >> )

  &name ( id -- str* )
    #21 ADD ,&id STR
    ;&sym JMP2r

  &pop ( -- )
    ,&ptr LDR2 #0001 SUB2 LDAk /name <create-symbol>
    ,&ptr STR2
    JMP2r

  &sym cebb &id $2
]
