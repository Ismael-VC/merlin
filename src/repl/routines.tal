(
@|repl/routines )

@REPL ( -> )
	(
	@|Read )
	.Console/read DEI

	DUP #1b NEQ ?{    ( 1b  \e  ^[  ^3  ESC -> ESCAPE )
		#00 ;&in-esc STA
		POP BRK }
	( DUP SP NEQ ?{ POP BRK ( ignore ) } )
	DUP #03 NEQ ?{    ( 03  ^C -> ETX )
		bye }
	DUP #04 NEQ ?{    ( 04  ^D -> EOT )
		bye }
	DUP #0c NEQ ?{    ( 0c  ^L -> FORM-FEED )
		pstr: \clear-console-screen \reset-console-cursor \0
		banner
		POP BRK }
	DUP LIT "@ NEQ ?{ #01 ;&expression STA }
	DUP #28 NEQ ?{ #00 ,&comment STR POP BRK }
	DUP #29 NEQ ?{ #01 ,&comment STR POP BRK }

	[ LIT &in-esc 01 ] ?{
		DUP LIT \r NEQ ?{
			#01 ,&in-esc STR
			#00 ;&multi-line STA } }

	DUP LIT \r NEQ ?{
		#01 ,&multi-line STR
		[ LIT &comment 01 ] ?{
			POP BRK }
		;input-ptr LDA2 STA
		;input-ptr LDA2 INC2 ;input-ptr STA2 BRK }

	[ LIT &multi-line 01 ] ?{
		emit BRK
		!&multiline-end

		.FORTH-PROMPT ?{
			!&no-multiline-prompt }

		~logger/info/multiline-prompt.tal

		.UNICODE-PROMPT ?{
			{ "... \s \0 } !&continue }
		{ \s "... \s \0 }

		&continue ( -- )
			STH2r pstr
		&no-multiline-prompt
			;input-ptr LDA2 STA
			;input-ptr LDA2 INC2 ;input-ptr STA2 BRK }

	emit BRK

	&multiline-end ( -- )
	#0000 ;length STA2
	~debugger/routines/before-eval.tal

	(
	@|Eval )
	;head-ptr LDA2 DUP2 ;prev-head STA2 STH2 ( | +prev-head* )
	<assemble> ;abort LDA ?&expr-abort
	~logger/info/summary.tal

	[ LIT &expression $1 ] ?{
		( assemble expr return jump )
		;head-ptr LDA2 STH2 ( | prev-head* +head* )
		[ LIT LIT2 ] STH2kr STA ( | prev-head* head* )
		;REPL/expr-return STH2kr INC2 STA2 ( | prev-head* -head* )
		[ LIT JMP2 ] STH2r INC2 INC2 INC2 STA ( | prev-head* )
		( reset head-ptr )
		STH2kr ;head-ptr STA2 ( | prev-head* )
		;&on-eval .Console/vector DEO2
		STH2r JMP2 } ( | -prev-head* )

	POP2r ( | -prev-head* )

	&expr-return ( -- )
		;REPL .Console/vector DEO2
		#00 ,&expression STR
		#0400 #0000 ;input mzero

	(
	@|Print )
	~debugger/routines/after-eval.tal
	~logger/info/stacks.tal

	(
	@|Loop )
	.FORTH-PROMPT ?{ <print-prompt> BRK }
	<\n> <print-prompt> BRK

&expr-abort ( | prev-head* -- )
	~logger/error/abort.tal
	( POP2r ( | -prev-head* ) )
	<>!
	#00 ;abort STA
	!REPL/expr-return

&on-eval ( -! )
	.Console/read DEI NL NEQ ?{
		[ LIT &count $1 ] INCk ,&count STR ( +count | )

		#02 NEQ ?{ ( -count | )
			#00 ,&count STR
			!&expr-abort }

		BRK }
	( reset ) #00 ,&count STR
	BRK

@<print-prompt> ( -- )
	pstr: \CSI "m \CSI "1;3;4m \CSI "48;2;51;00;34m \CSI "38;2;119;34;119m \0
	.FORTH-PROMPT ?{
		;&ok !&continue2 }
	.UNICODE-PROMPT ?{
		;&prompt-start-unicode !&continue }

	;&prompt-start

	&continue
		pstr
		~logger/info/prompt.tal
		;&prompt-end

	&continue2
		pstr
		pstr: \CSI "m \CSI "48;2;51;00;34m \CSI "38;2;170;170;170m \n \0
		JMP2r

	&ok [ \s \s \s "ok \n \r \0 ]
	&prompt-start         [ "uxn \0 ]
	&prompt-start-unicode [ "Öƒ \s \0 ]
	&prompt-end           [ "> \s \0 ]


@<print-banner>
	;banner !pstr
