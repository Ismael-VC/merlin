(
@|repl/routines )

@REPL ( -> )
	(
	@|Read )
	.Console/read DEI

	DUP #7f NEQ ?{
		#08 emit BRK }

	DUP #1b NEQ ?{    ( 1b  \e  ^[  ^3  ESC -> ESCAPE )
		#00 ;&in-esc STA
		POP BRK }
	( DUP SP NEQ ?{ POP BRK ( ignore ) } )
	DUP #03 NEQ ?{    ( 03  ^C -> ETX )
		#01 exit }
	DUP #04 NEQ ?{    ( 04  ^D -> EOT )
		bye }
	DUP #0c NEQ ?{    ( 0c  ^L -> FORM-FEED )
		pstr: \reset-console-cursor \clear-console-screen \0
		<print-pre-prompt>
		POP BRK }
	DUP LIT "@ NEQ ?{ #01 ;&expression STA }
	DUP #28 NEQ ?{ #00 ;&comment STA }
	DUP #29 NEQ ?{ #01 ;&comment STA }

	[ LIT &in-esc 01 ] ?{
		DUP LIT \r NEQ ?{
			#01 ,&in-esc STR
			#00 ;&multi-line STA } }

	[ LIT &multi-line 01 ] ?{
		#01 ,&multi-line STR
		pstr: \reset-console-style \bold \bg-0 \fg-3 \0
		~logger/info/multiline-prompt.tal

		{ \n \r "... \s \0 }

		&continue ( -- )
			STH2r pstr
			pstr: \reset-console-style \bg-0 \fg-1 \0
			BRK }

	DUP LIT \r NEQ ?{
		#01 ;&multi-line STA
		[ LIT &comment 01 ] ?{
			POP BRK }
		<\n> emit !&multiline-end }

	&no-multiline-prompt
	DUP emit
	;input-ptr LDA2 STA
	;input-ptr LDA2 INC2 ;input-ptr STA2 BRK

	&multiline-end ( -- )
	#0000 ;length STA2
	~debugger/routines/before-eval.tal

	(
	@|Eval )
	;head-ptr LDA2 DUP2 ;prev-head STA2 STH2 ( | +prev-head* )
	<assemble> ;abort LDA ?&expr-abort
	~logger/info/summary.tal

	[ LIT &expression $1 ] ?{
		( assemble expr return jump )
		;head-ptr LDA2 STH2 ( | prev-head* +head* )
		[ LIT LIT2 ] STH2kr STA ( | prev-head* head* )
		;REPL/expr-return STH2kr INC2 STA2 ( | prev-head* -head* )
		[ LIT JMP2 ] STH2r INC2 INC2 INC2 STA ( | prev-head* )
		( reset head-ptr )
		STH2kr ;head-ptr STA2 ( | prev-head* )
		;&on-eval .Console/vector DEO2
		STH2r JMP2 } ( | -prev-head* )

	POP2r ( | -prev-head* )

	&expr-return ( -- )
		;REPL .Console/vector DEO2
		#00 ,&expression STR
		#0400 #0000 ;input mzero

	(
	@|Print )
	<print-post-prompt>
	~debugger/routines/after-eval.tal
	~logger/info/stacks.tal

	(
	@|Loop )
	<print-pre-prompt> BRK

&expr-abort ( | prev-head* -- )
	~logger/error/abort.tal
	( POP2r ( | -prev-head* ) )
	<>!
	#00 ;abort STA
	!REPL/expr-return

&on-eval ( -! )
	.Console/read DEI NL NEQ ?{
		[ LIT &count $1 ] INCk ,&count STR ( +count | )

		#02 NEQ ?{ ( -count | )
			#00 ,&count STR
			!&expr-abort }

		BRK }
	( reset ) #00 ,&count STR
	BRK

@<print-pre-prompt> ( -- )
	pstr: \CSI "m \bold \bg-0 \fg-3 \0


	( ;input ;input-ptr LDA2 NEQ2 ?{
		pstr: \console-cursor-up \0 } )

	;&prompt-start pstr
	~logger/info/prompt.tal
	;&prompt-end pstr
	pstr: \CSI "m \CSI "48;2;51;00;34m \CSI "38;2;170;170;170m \0
	JMP2r

	&prompt-start [ \r "Öƒ \s \0 ]
	&prompt-end   [ "> \s \0 ]

@<print-post-prompt> ( -- )
	;ok !pstr

@<print-banner>
	;banner !pstr

@help ( -- )
